// Arm Wind Generator Script

piece base, post, blades;

static-var built, lastfanspeed, lastdir;

#define SMOKEPIECE1 base

#include "StateChg.h"
#include "smokeunit.h"
#include "exptype.h"

Go()
	{
	spin blades around z-axis speed lastfanspeed;
	turn post to y-axis lastdir speed <20>;
	}

Stop()
	{
	spin blades around z-axis speed <0>;
	turn post to y-axis lastdir speed <0>;
	}

#define ACTIVATECMD call-script Go();
#define DEACTIVATECMD call-script Stop();
#include "StateChg.h"

Create()
	{
	dont-cache blades;
	dont-cache post;
	dont-shade blades;
	dont-shade post;
	dont-shade base;
	lastfanspeed = <10>;
	lastdir = 0;
	call-script InitState();
	start-script SmokeUnit();
	}

Activate()
	{
	start-script RequestState( ACTIVE );
	}

Deactivate()
	{
	start-script RequestState( INACTIVE );
	}

SetSpeed(fanspeed)
	{
	if( (get BUILD_PERCENT_LEFT)==0 )
		{
		lastfanspeed = fanspeed;
		spin blades around z-axis speed fanspeed;
		}
	}

SetDirection(dir)
	{
	if( (get BUILD_PERCENT_LEFT)==0 )
		{
		lastdir = dir;
		turn post to y-axis dir speed <20>;
		}
	}

SweetSpot(piecenum)
	{
	piecenum=0;
	}

Killed( severity, corpsetype )
	{
	if (severity <= 25)
		{
		corpsetype = 1;
		explode base type	BITMAPONLY | BITMAP1;
		explode blades type	BITMAPONLY | BITMAP2;
		explode post type	BITMAPONLY | BITMAP3;
		}

	if (severity <= 50)
		{
		corpsetype = 2;
		explode base type	BITMAPONLY | BITMAP1;
		explode blades type	FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2;
		explode post type	BITMAPONLY | BITMAP3;
		return( 0 );
		}

	if (severity <= 99)
		{
		corpsetype = 3;
		explode base type	BITMAPONLY | BITMAP1;
		explode blades type	SHATTER | BITMAP2;
		explode post type	BITMAPONLY | BITMAP3;
		return( 0 );
		}

	corpsetype = 3;
	explode base type	BITMAPONLY | BITMAP1;
	explode blades type	SHATTER | BITMAP2;
	explode post type	BITMAPONLY | BITMAP3;
	return( 0 );
	}